function prompt_char {
  if git branch >/dev/null 2>/dev/null; then
    echo "±"
  else
    echo "○"
  fi
}

function local_ruby {
  if cat .ruby-version > /dev/null 2>/dev/null; then
     echo " using ${_prompt_praveen_colors[6]}$(cat .ruby-version)%f"
  fi
}

function prompt_praveen_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_praveen_colors[1]}%b%f%u%c${_prompt_praveen_colors[4]}●%f)"
  else
    branch_format="(${_prompt_praveen_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  # Define prompts.
  PROMPT="
%(?.${_prompt_praveen_colors[3]}%n%f.%F{red}%n%f) at ${_prompt_praveen_colors[2]}%m%f$(local_ruby) %fin ${_prompt_praveen_colors[5]}%~%f "'${vcs_info_msg_0_}'"
""$(prompt_char) "
  RPROMPT=''
}

function prompt_praveen_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_praveen_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_praveen_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{160}" # Hotpink
      "%F{118}" # Limegreen
      "%F{161}" # Dark Pink
    )
  else
    _prompt_praveen_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{red}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_praveen_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_praveen_colors[5]}%a%f)"
  local unstaged_format="${_prompt_praveen_colors[2]}!%f"
  local staged_format="${_prompt_praveen_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:versopm' format '(%v)'

}

prompt_praveen_setup "$@"
